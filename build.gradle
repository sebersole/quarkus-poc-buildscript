plugins {
    id 'java'
    id 'java-gradle-plugin'

    id 'com.gradle.plugin-publish' version '0.12.0'

    // for publishing snapshots
    id 'maven-publish'
    id 'org.hibernate.build.maven-repo-auth' version '3.0.4'
}

group 'com.github.sebersole.quarkus'
version '0.9'

repositories {
    mavenCentral()
}

ext {
    pluginId = 'sebersole.quarkus.plugin-poc'

    // this would come from the build when integrated upstream
    quarkusVersion = '1.7.1.Final'

    junit5Version = '5.3.1'
}

configurations {
    quarkusPlatforms {
        description = "Configuration for managing Quarkus platforms"
    }
    implementation.extendsFrom( quarkusPlatforms )
    testImplementation.extendsFrom( quarkusPlatforms )
    testRuntimeOnly.extendsFrom( quarkusPlatforms )
}

dependencies {
    quarkusPlatforms enforcedPlatform( "io.quarkus:quarkus-bom:${project.quarkusVersion}" )

//    implementation enforcedPlatform( "io.quarkus:quarkus-bom:${project.quarkusVersion}" )
    implementation 'org.jboss:jandex'

//    testImplementation enforcedPlatform( "io.quarkus:quarkus-bom:${project.quarkusVersion}" )
    testImplementation "org.junit.jupiter:junit-jupiter-api:${project.junit5Version}"
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'

//    testRuntimeOnly enforcedPlatform( "io.quarkus:quarkus-bom:${project.quarkusVersion}" )
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${project.junit5Version}"
}

gradlePlugin {
    plugins {
        quarkusPlugin {
            id = project.pluginId
            implementationClass = 'com.github.sebersole.gradle.quarkus.QuarkusPlugin'
        }
    }
}

pluginBundle {
    ext {
        pluginUrl = 'https://github.com/sebersole/quarkus-gradle-plugin-poc'
        pluginDescription = 'Proof-of-concept Gradle plugin for builds using Quarkus'
    }
    website = ext.pluginUrl
    vcsUrl = ext.pluginUrl
    description = ext.pluginDescription
    tags = ['quarkus', 'graalvm']

    plugins {
        quarkusPlugin {
            // id is captured from java-gradle-plugin configuration
            displayName = pluginDescription
        }
    }
}

javadoc {
    options.addStringOption( 'Xdoclint:none', '-quiet' )
}

tasks.test {
    useJUnitPlatform()
}

sourceSets {
    test {
        resources.srcDirs += 'src/test/projects'
    }
}


// for publishing snapshots

if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
    tasks.publishPlugins.enabled = false
}
else {
    tasks.publish.enabled = false
}

publishing {
    publications {
        publishedArtifacts( MavenPublication ) {
            from components.java
        }
    }

    repositories {
        maven {
            name 'jboss-snapshots-repository'
            url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
        }
    }
}
